options {
 STATIC = false;
}

PARSER_BEGIN(RVCParser)
package RVC.parser;

import java.util.ArrayList;
import java.util.HashMap;
import java.util.HashSet;
import java.util.Scanner;
import java.io.*;

public class RVCParser {

  public static void main(String[] args){
     Scanner sc = new Scanner(System.in);
     StringBuilder buf = new StringBuilder();
     while(sc.hasNextLine()) buf.append(sc.nextLine());
     RVCParser rvcParser = parse(buf.toString()); 
     System.out.println(rvcParser.specName + "\n==========");
     System.out.println(rvcParser.events + "\n==========" );
     System.out.println(rvcParser.formalism  + "\n==========");
     System.out.println(rvcParser.formula  + "\n==========");
     System.out.println(rvcParser.handlers  + "\n==========");
  }

  public static RVCParser parse(String input) {
    Reader reader = new StringReader(input);
	  RVCParser rvcParser = new RVCParser(reader);	
	 
	  rvcParser.events = new HashMap<String, String>();
	  rvcParser.handlers = new HashMap<String, String>();

    try{
      rvcParser.Start();
    }
	 catch(ParseException e){
      System.err.println(e.getMessage());
	 }
	 catch(TokenMgrError e){
      System.err.println(e.getMessage());
	 }
	 return rvcParser;
  }

  private String specName;
  //Domain is event name, range is body
  private HashMap<String, String> events;
  //Domain is category, range is body
  private HashMap<String, String> handlers;
  private String formalism;
  private String formula;
  
  public HashMap<String, String> getEvents (){
	  return events;
  }
  
  public HashMap<String, String> getHandlers () {
	  return handlers;
  }

  public String getFormalism () {
	  return formalism;
  }

  public String getFormula () {
	  return formula;
  }
  private String parseFormula () throws ParseException {
		
    StringBuilder buf = new StringBuilder();

    try {
   			char next = jj_input_stream.readChar();
        while (true){
          if(next == '@'){
            jj_input_stream.backup(1);
            break;
          }
          buf.append(next);
          next = jj_input_stream.readChar();
        }
    } catch (Exception e) {
    		if(e instanceof ParseException)
				throw (ParseException)e;
    		Token t = new Token();
    		t.beginLine = jj_input_stream.getBeginLine();
    		t.beginColumn = jj_input_stream.getBeginColumn();    		
    		throw new ParseException("formula");
    }
    return buf.toString().trim();
  }

  private String parseCodeBlock () throws ParseException {
    StringBuilder buf = new StringBuilder();

    try {
   			char next = jj_input_stream.readChar();
    		int nesting = 0;
        while (true){
          if(next == '{'){
            ++nesting;
            buf.append(next);
          }
          else if(next == '}'){
            --nesting;
            if(nesting != -1){
              buf.append(next);
            }
            else {
              jj_input_stream.backup(1);
              break;
            } 
          }
          else {
            buf.append(next);
          }
          next = jj_input_stream.readChar();
        }
    } catch (Exception e) {
    		if(e instanceof ParseException)
				throw (ParseException)e;
    		Token t = new Token();
    		t.beginLine = jj_input_stream.getBeginLine();
    		t.beginColumn = jj_input_stream.getBeginColumn();    		
    		throw new ParseException("bad code block");
    }
    return buf.toString().trim();
  }
}
PARSER_END(RVCParser)

SKIP : {
 	<WHITESPACE: [" ","\t","\r","\n"]>
}
TOKEN : {
    <LBRACE : "{">
	| <RBRACE : "}">
	| <AT : "@">
	| <EVENT : "event">
	| <ID : (<LETTER>|"_")(<LETTER>|<DIGIT>|"_")*> 
	| <DIGIT : ["0"-"9"]>
	| <LETTER : ["a"-"z","A"-"Z"]>
}

void Start() : {
  Token formalismToken;
  Token specNameToken;
}
{ 
  specNameToken = <ID> {specName = specNameToken.image;}
  "{"
    (Event())+
    formalismToken = <ID>
    {formalism = formalismToken.image; }
        ":" 
    { formula = parseFormula(); }
    (Handler())+ 
  "}"
  <EOF>
}

void Event() : {
  Token token;
  String code;
} 
{
  "event"
  token = <ID> "{"
    { code = parseCodeBlock(); }
  "}"
  { events.put(token.image,code); }
}

void Handler() : {
  Token token;
  String code;
} 
{
  "@"
  token = <ID> "{"
    { code = parseCodeBlock(); }  
  "}"
  { handlers.put(token.image,code); }
}
